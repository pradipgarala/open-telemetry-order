plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.6'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

group = 'com.example'
version = '0.0.6-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

bootJar {
    archiveFileName = "app.jar"
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    agent
}

repositories {
    mavenCentral()
}

dependencies {
    agent "io.opentelemetry.javaagent:opentelemetry-javaagent:1.32.0"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}

task copyAgent(type: Copy) {
    from configurations.agent {
        rename "opentelemetry-javaagent-.*\\.jar", "opentelemetry-javaagent.jar"
    }
    into layout.buildDirectory.dir("agent")
}

bootJar {
    dependsOn copyAgent

    archiveFileName = "app.jar"
}

jib {
//    from.image = 'amazoncorretto:17.0.9-alpine'
    from {
        image = 'gcr.io/distroless/java17-debian12'

        platforms {
//            platform {
//                architecture = 'amd64'
//                os = 'linux'
//            }
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }

    extraDirectories {
        paths {
            path {
                from = layout.buildDirectory.dir("agent")
                into = '/otelagent'
            }
        }
    }

    container {
        jvmFlags = [
                '-javaagent:/otelagent/opentelemetry-javaagent.jar'
        ]
    }
}

tasks.named('jibDockerBuild').configure {
    dependsOn copyAgent
}